<!DOCTYPE html>
<html>
<head>
<title>Providing documentation with Docsonnet - Jsonnet Training Course</title>
<meta charset="utf-8">
<meta name="viewport" content="width=device-width, initial-scale=1.0">
<meta name="generator" content="md2html">
<link rel="stylesheet" type="text/css" href="assets/style.css">
<script src="https://cdn.jsdelivr.net/gh/google/code-prettify@master/loader/run_prettify.js"></script>
<script src="assets/prettyprint.js"></script>
<script src="assets/anchorlinks.js"></script>
<script src="assets/toc.js"></script>
</head>
<body>

<main>
<p><span class="nav previous"><a href="lesson4.html">« previous</a></span> <span class="nav index"><a href="index.html">index</a></span></p>
<h1>Providing documentation with Docsonnet</h1>
<p><a href="https://github.com/jsonnet-libs/docsonnet">Docsonnet</a> provides a way to consistently
add docstrings to Jsonnet code. As Docsonnet docstrings are written as Jsonnet
attributes, it can also natively render user-friendly documentation in Markdown.</p>
<h2>Objectives</h2>
<ul>
<li>Write docstrings for Docsonnet</li>
<li>Generate markdown documentation</li>
</ul>
<h2>Lesson</h2>
<h3>Writing docstrings</h3>
<p>We'll continue with the webserver library from the exercise.</p>
<pre><code class="language-jsonnet">local k = import 'k.libsonnet';

{
  new(name, replicas=1): {
    container::
      k.core.v1.container.new('httpd', 'httpd:2.4'),

    deployment:
      k.apps.v1.deployment.new(
        name,
        replicas,
        [self.container]
      ),
  },

  withImage(image): {
    container+:
      k.core.v1.container.withImage(image),
  },
}

// example1/example1.jsonnet
</code></pre>
<p>This library provides a number of functions to create a webserver. In docsonnet lingo,
this is called a 'package'. Each function has a few arguments which usually have a fixed
type. As Jsonnet itself is not a typed language, we'll simply try to document the type.</p>
<hr>
<p>To get started, let's grab docsonnet's doc-util library. It provides a few functions to
write consistent docstrings for the different objects in Jsonnet.</p>
<p><code>$ jb install github.com/jsonnet-libs/docsonnet/doc-util</code></p>
<p>Note that the <a href="https://github.com/jsonnet-libs/docsonnet/blob/master/doc-util/README.md">docs for
doc-util</a> are
also rendered by docsonnet.</p>
<hr>
<pre><code class="language-jsonnet">local d = import 'github.com/jsonnet-libs/docsonnet/doc-util/main.libsonnet';
local k = import 'k.libsonnet';

{
  '#':: d.pkg(
    name='webserver',
    url='github.com/jsonnet-libs/jsonnet-training-course/lessons/lesson5/example1',
    help='`webserver` provides a basic webserver on Kubernetes',
    filename=std.thisFile,
  ),

  new(name, replicas=1): {
    container::
      k.core.v1.container.new('httpd', 'httpd:2.4'),

    deployment:
      k.apps.v1.deployment.new(
        name,
        replicas,
        [self.container]
      ),
  },

  withImage(image): {
    container+:
      k.core.v1.container.withImage(image),
  },
}

// example1/example2.jsonnet
</code></pre>
<p>The docsonnet puts a claim on keys that start with a hash <code>#</code>. It assumes that keys
starting with a hash symbol is very uncommon. Additionally, it closely relates to how
comments are written.</p>
<p>The package definition is put in the <code>#</code> key without a value.</p>
<p>The <code>url</code> argument is the part that can be used to install this library with
jsonnet-bundler:</p>
<p><code>$ jb install &lt;url&gt;</code></p>
<p>In combination with the <code>url</code>, the <code>filename</code> refers to what should be imported, note the
neat <code>std.thisFile</code> shortcut so we don't have to remember to change the filename here if
we ever rename the file.</p>
<p><code>import '&lt;url&gt;/&lt;filename&gt;'</code></p>
<hr>
<pre><code class="language-jsonnet">local d = import 'github.com/jsonnet-libs/docsonnet/doc-util/main.libsonnet';
local k = import 'k.libsonnet';

{
  '#':: d.pkg(
    name='webserver',
    url='github.com/jsonnet-libs/jsonnet-training-course/lessons/lesson5/example1',
    help='`webserver` provides a basic webserver on Kubernetes',
    filename=std.thisFile,
  ),

  '#new':: d.fn(
    help=|||
      `new` creates a Deployment object for Kubernetes

      * `name` sets the name for the Deployment object
      * `replicas` number of desired pods, defaults to 1
    |||,
    args=[
      d.arg('name', d.T.string),
      d.arg('replicas', d.T.number, 1),
    ]
  ),
  new(name, replicas=1): {
    container::
      k.core.v1.container.new('httpd', 'httpd:2.4'),

    deployment:
      k.apps.v1.deployment.new(
        name,
        replicas,
        [self.container]
      ),
  },

  '#withImage':: d.fn(
    help='`withImage` modifies the image used for the httpd container',
    args=[
      d.arg('image', d.T.string),
    ]
  ),
  withImage(image): {
    container+:
      k.core.v1.container.withImage(image),
  },
}

// example1/example3.jsonnet
</code></pre>
<p>Docstrings for other elements are put in the same key prefixed by a hash <code>#</code>, for example
the docstring for <code>new()</code> will be in the <code>#new</code> key. Functions can be documented with
<code>d.fn(help, args)</code>.</p>
<p>As the <code>help</code> text is quite long for this example, we're using multi line strings with
<code>|||</code>. This also improves the readability in the code. The <code>args</code> can be typed, values
for these are provided by <code>d.T.&lt;type&gt;</code> to increase consistency.</p>
<hr>
<p>For the sake of this lesson, let's add a new <code>images</code> object with a few attributes:</p>
<pre><code class="language-jsonnet">local d = import 'github.com/jsonnet-libs/docsonnet/doc-util/main.libsonnet';
local k = import 'k.libsonnet';

{
  '#':: d.pkg(
    name='webserver',
    url='github.com/jsonnet-libs/jsonnet-training-course/lessons/lesson5/example1',
    help='`webserver` provides a basic webserver on Kubernetes',
    filename=std.thisFile,
  ),

  images: {
    apache: 'httpd:2.4',
    nginx: 'nginx:1.22',
  },

  '#new':: d.fn(
    help=|||
      `new` creates a Deployment object for Kubernetes

      * `name` sets the name for the Deployment object
      * `replicas` number of desired pods, defaults to 1
    |||,
    args=[
      d.arg('name', d.T.string),
      d.arg('replicas', d.T.number, 1),
    ]
  ),
  new(name, replicas=1): {
    container::
      k.core.v1.container.new('httpd', 'httpd:2.4'),

    deployment:
      k.apps.v1.deployment.new(
        name,
        replicas,
        [self.container]
      ),
  },

  '#withImage':: d.fn(
    help='`withImage` modifies the image used for the httpd container',
    args=[
      d.arg('image', d.T.string),
    ]
  ),
  withImage(image): {
    container+:
      k.core.v1.container.withImage(image),
  },
}

// example1/example4.jsonnet
</code></pre>
<p>The <code>images</code> key holds an object with additional webserver images.</p>
<p>It can be used in combination with <code>withImage</code>:</p>
<p><code>webserver.withImage(webserver.images.nginx)</code></p>
<hr>
<pre><code class="language-jsonnet">local d = import 'github.com/jsonnet-libs/docsonnet/doc-util/main.libsonnet';
local k = import 'k.libsonnet';

{
  '#':: d.pkg(
    name='webserver',
    url='github.com/jsonnet-libs/jsonnet-training-course/lessons/lesson5/example1',
    help='`webserver` provides a basic webserver on Kubernetes',
    filename=std.thisFile,
  ),

  '#images':: d.obj(
    help=|||
      `images` provides images for common webservers

      Usage:

      ```
      webserver.new('my-nginx')
      + webserver.withImage(webserver.images.nginx)
      ```
    |||
  ),
  images: {
    '#apache':: d.val(d.T.string, 'Apache HTTP webserver'),
    apache: 'httpd:2.4',
    '#nginx':: d.val(d.T.string, 'Nginx HTTP webserver'),
    nginx: 'nginx:1.22',
  },

  '#new':: d.fn(
    help=|||
      `new` creates a Deployment object for Kubernetes

      * `name` sets the name for the Deployment object
      * `replicas` number of desired pods, defaults to 1
    |||,
    args=[
      d.arg('name', d.T.string),
      d.arg('replicas', d.T.number, 1),
    ]
  ),
  new(name, replicas=1): {
    container::
      k.core.v1.container.new('httpd', 'httpd:2.4'),

    deployment:
      k.apps.v1.deployment.new(
        name,
        replicas,
        [self.container]
      ),
  },

  '#withImage':: d.fn(
    help='`withImage` modifies the image used for the httpd container',
    args=[
      d.arg('image', d.T.string),
    ]
  ),
  withImage(image): {
    container+:
      k.core.v1.container.withImage(image),
  },
}

// example1/example5.jsonnet
</code></pre>
<p>Just like with functions, the key for the docstring gets prefixed by <code>#</code>. Objects can be
documented with <code>d.obj(help)</code>.</p>
<p>The attributes in this object can be documented with <code>d.val(type, help)</code>.</p>
<h3>Generating markdown docs</h3>
<p>The doc-util library has a built-in rendering:</p>
<pre><code class="language-jsonnet">local d = import 'github.com/jsonnet-libs/docsonnet/doc-util/main.libsonnet';

d.render(import 'example5.jsonnet')

// example1/example7.jsonnet
</code></pre>
<p>The <code>render(obj)</code> function returns a format to output <a href="https://jsonnet.org/learning/getting_started.html#multi">multiple
files</a>.</p>
<hr>
<pre><code>{
  'README.md': &quot;...&quot;,
  'path/to/example.md': &quot;...&quot;,
}
</code></pre>
<hr>
<p>Jsonnet can export those files:</p>
<p><code>$ jsonnet --string --create-output-dirs --multi docs/ example7.jsonnet</code></p>
<ul>
<li><code>--string</code> because the Markdown output should be treated as a string instead of JSON.</li>
<li><code>--create-output-dirs</code> ensure directories for <code>path/to/example.md</code> get created.</li>
<li><code>--multi docs/</code> to set the output directory for multiple files to <code>docs/</code>.</li>
</ul>
<p>Or in short:</p>
<p><code>$ jsonnet -S -c -m docs/ example7.jsonnet</code></p>
<p>Note that this overwrites but does not remove existing files.</p>
<pre><code class="language-makefile">.PHONY: docs
docs:
    rm -rf docs/ &amp;&amp; \
    jsonnet -J ./vendor -S -c -m docs/ example7.jsonnet

// example1/Makefile
</code></pre>
<p>A simple Makefile target can be quite useful to contain these incantations.</p>
<hr>
<blockquote>
<p>This can also be done without the additional Jsonnet file by using <code>jsonnet --exec</code>:</p>
<p><code>$ jsonnet -S -c -m docs/ --exec &quot;(import 'doc-util/main.libsonnet').render(import 'example7.jsonnet')&quot;</code></p>
</blockquote>
<hr>
<p>The documentation for the webserver library will look like this:</p>
<pre><code class="language-markdown"># package webserver

`webserver` provides a basic webserver on Kubernetes

## Install

```
jb install github.com/jsonnet-libs/jsonnet-training-course/lessons/lesson5/example1@master
```

## Usage

```jsonnet
local webserver = import &quot;github.com/jsonnet-libs/jsonnet-training-course/lessons/lesson5/example1/example5.jsonnet&quot;
```

## Index

* [`fn new(name, replicas=1)`](#fn-new)
* [`fn withImage(image)`](#fn-withimage)
* [`obj images`](#obj-images)

## Fields

### fn new

```ts
new(name, replicas=1)
```

`new` creates a Deployment object for Kubernetes

* `name` sets the name for the Deployment object
* `replicas` number of desired pods, defaults to 1


### fn withImage

```ts
withImage(image)
```

`withImage` modifies the image used for the httpd container

### obj images

`images` provides images for common webservers

Usage:

```
webserver.new('my-nginx')
+ webserver.withImage(webserver.images.nginx)
```


* `images.apache` (`string`): `&quot;httpd:2.4&quot;` - Apache HTTP webserver
* `images.nginx` (`string`): `&quot;nginx:1.22&quot;` - Nginx HTTP webserver

// example1/docs/README.md
</code></pre>
<h2>Conclusion</h2>
<p>Providing documentation can be very helpful to communicate the library's intended
use. The argument types give an indication about the expected values and the help
text can contain code samples to get meaningful results quickly.</p>
<p>Rendering documentation can be done directly with Jsonnet without additional
programs, altough the incantations may feel like magic at first.</p>
<p><span class="nav previous"><a href="lesson4.html">« previous</a></span> <span class="nav index"><a href="index.html">index</a></span></p>
</main>

<footer>
    <p>
        <a href="about.html">About</a>
        | <a href="https://github.com/jsonnet-libs/jsonnet-training-course">Source</a>
    </p>
</footer>
</body>
</html>

